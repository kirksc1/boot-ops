configure(allprojects.findAll {it.name != 'boot-ops-sample'}) {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    group 'com.github.kirksc1.bootops'
    version '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/kirksc1/boot-ops"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}

ext{
    //dependencies
    springBootVersion = '3.5.4'
    lombokVersion = '1.18.38'
}

dependencies {
    implementation project(':boot-ops-cli')
    implementation project(':boot-ops-converge')
    implementation project(':boot-ops-core')
    implementation project(':boot-ops-jackson')
    implementation project(':boot-ops-validate')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testRuntimeOnly('jakarta.el:jakarta.el-api:4.0.0')
    testRuntimeOnly('org.glassfish:jakarta.el:4.0.2')

    testImplementation("org.springframework.boot:spring-boot-starter-test:$rootProject.springBootVersion")
}

tasks.register("codeCoverageReport", JacocoReport) {
    reports {
        xml.required = true
    }
    subprojects {
        subproject -> subproject.plugins.withType(JacocoPlugin).configureEach {
            subproject.tasks.matching({t -> t.extensions.findByType(JacocoTaskExtension) }).configureEach {
                testTask ->
                sourceSets subproject.sourceSets.main
                executionData(testTask)
            }

            subproject.tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).forEach {
                rootProject.tasks.codeCoverageReport.dependsOn(it)
            }
        }
    }
}